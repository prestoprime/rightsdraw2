#!/bin/tcsh -f
#  rightsdraw
#  Version: 2.0.1
#  Authors: L. Boch
#
#  Copyright (C) 2010-2012 RAI â€“ Radiotelevisione Italiana <cr_segreteria@rai.it>
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Affero General Public License for more details.
#
# You should have received a copy of the GNU Affero General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.


set echo_style = both
set mykpr = `cat $pdir/users/$puser/config/mykpr`
echo "content-type: text/html\n"
set this = `basename $0`
if ( $REQUEST_METHOD != "GET" ) then
	echo "400"
	echo "Bad request "
	echo "$this accept only GET"
	exit 99
endif
	

set inquery = `echo $QUERY_STRING | cut -d'&' -f1- --output-delimiter=' ' | cut -d'=' -f1- --output-delimiter=' '`
set inquery = `$pdir/bin/clean.querystring $inquery`
set form = "null"
set i = 1
while ( $i < $#inquery )
        if ( "$inquery[$i]" == "form" ) then
                @ i++
                set form = "$inquery[$i]"
        else
		echo "400"
		echo "Bad request "
		echo "$this unknown $inquery[$i]"
                exit 1
        endif
        @ i++
end
#Sanity Check
if ( !(-ersf $pdir/share/$form) ) then
	echo "404"
	echo "Not Found $form"
	exit 2
endif
if ( $form != "query-kpr-form.html" ) then
	cat $pdir/share/$form
else
	set mylist = `setenv QUERY_STRING "folder=$mykpr" ; $pdir/www/cgi-bin/kpr/list | awk 'FNR>1'`
	set ret = $?
	if ( $ret ) then
	        echo "500"
	        echo "Server Error"
		echo "$this got $ret from listing kp from $mykpr"
	else if ( "$mylist[1]" != 200 ) then
		echo "$mylist"
	else
		#Got Success
		set loop = 3
		set options = ""
		while ( $loop <= $#mylist )
			set options = "$options<option>$mylist[$loop]<\/option>"
			@ loop++
		end
		cat $pdir/share/$form | sed -e s/'+++kproptions+++'/$options/g
	endif
endif
exit 0
