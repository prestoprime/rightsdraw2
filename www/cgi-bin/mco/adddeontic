#!/bin/tcsh -f
#  rightsdraw
#  Version: 2.0.2
#  Authors: L. Boch
#
#  Copyright (C) 2010-2013 RAI - Radiotelevisione Italiana <cr_segreteria@rai.it>
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Affero General Public License for more details.
#
# You should have received a copy of the GNU Affero General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

set echo_style = both
echo "content-type: text/plain\n"
set mcomode = `cat $pdir/users/$puser/config/mcomode`
set repo = $pdir/users/$puser/repo/$mcomode
set colors = `cat $pdir/users/$puser/config/colors`
set rankdir = `cat $pdir/users/$puser/config/rankdir`
set this = `basename $0`
if ( $REQUEST_METHOD != "GET" ) then
	echo "400"
	echo "Bad request "
	echo "$this accept only GET"
	exit 99
endif
	

set inquery = `echo $QUERY_STRING | cut -d'&' -f1- --output-delimiter=' ' | cut -d'=' -f1- --output-delimiter=' '`
set inquery = `$pdir/bin/clean.querystring $inquery`
set instance = "null"
set ind = "null"
set class = "null"
set handle = "null"
set i = 1
while ( $i < $#inquery )
        if ( "$inquery[$i]" == "instance" ) then
                @ i++
                set instance = "$inquery[$i]"
        else if ( "$inquery[$i]" == "ind" ) then
                @ i++
                set ind = "$inquery[$i]"
        else if ( "$inquery[$i]" == "class" ) then
                @ i++
                set class = "$inquery[$i]"
        else if ( "$inquery[$i]" == "handle" ) then
                @ i++
                set handle = "$inquery[$i]"
        else
		echo "400"
		echo "Bad request "
		echo "$this unknown $inquery[$i]"
                exit 1
        endif
        @ i++
end
set ret = 0
#Sanity check
if ( !( -e "$repo/$instance" ) ) then
	echo "404"
	echo "Not Found "
	echo "$this $repo/$instance"
	exit 2
endif
if ( "$class" == "null" || "$handle" == "null" ) then
	echo "400"
	echo "Bad request "
	echo "$this instance=$instance, handle=$handle, class=$class, ind=$ind"
	exit 3
endif
if ( "$ind" == "null" ) then
	set xnum = `cat $pdir/users/$puser/config/inumber|bc`
	@ xnum ++
	echo $xnum > $pdir/users/$puser/config/inumber
	set ind = "#x$xnum"
endif
# making a bak copy
cp $repo/$instance $repo/.tmp/$instance.$$.bak
# First adding deontic
xsltproc --stringparam twoclass $class --stringparam twoname $ind $pdir/xsl/addIndividual.xsl $repo/$instance > $repo/.tmp/$instance
set ret = $?
if ( $ret ) then
	echo "500"
	echo "Server Error"
	echo "$this failed on addIndividual ($class, $ind) to $instance"
	cp $repo/.tmp/$instance.$$.bak $repo/$instance
	exit 5
endif
mv $repo/.tmp/$instance $repo/$instance
# Then adding obj
set obj = "urn:mpeg:mpeg21:mco:core:2012#issuedIn"
set class = "urn:mpeg:mpeg21:mco:core:2012#Contract"
xsltproc --stringparam obj $obj --stringparam one $ind --stringparam twoclass $class --stringparam twoname $handle  $pdir/xsl/addObjProperty.xsl $repo/$instance > $repo/.tmp/$instance
set ret = $?
if ( $ret ) then
	echo "500"
	echo "Server Error"
	echo "$this failed on addObjProperty ($obj, $ind, $class, $handle) to $instance"
	cp $repo/.tmp/$instance.$$.bak $repo/$instance
	exit 5
endif
mv $repo/.tmp/$instance $repo/$instance
echo "200"
echo "Success"
exit 0





