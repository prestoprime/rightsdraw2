#!/bin/tcsh -f
#  rightsdraw
#  Version: 2.0.1
#  Authors: L. Boch
#
#  Copyritght (C) 2012-2013 RAI â€“ Radiotelevisione Italiana <cr_segreteria@rai.it>
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Affero General Public License for more details.
#
# You should have received a copy of the GNU Affero General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

set echo_style = both
echo "content-type: text/html\n"
set repo = $pdir/www/html/kpr
set this = `basename $0`
if ( $REQUEST_METHOD != "POST" ) then
	echo "400"
	echo "Bad request "
	echo "$this accept only POST"
	exit 99
endif
if ( $CONTENT_LENGTH > $securesize ) then
	echo "413"
	echo "Request Entity Too Large"
	exit 1
endif
#Getting this from multipart
set indata = $pdir/logs/$this.$$
cat > $indata
set folder = "test"
set mpsep = `head -n 1 $indata|sed 's/.$//'`  #to remove hidden CR
set folderpointer = `grep -n "Content-Disposition" $indata | grep 'name="folder"'|cut -d':' -f1`
if ( $folderpointer != "" ) then
	@ folderpointer += 2
	set folder = `head -n $folderpointer $indata | tail -n1|sed 's/.$//'`
endif

set instancepointer = `grep -n "Content-Disposition" $indata | grep 'name="instance"'|cut -d':' -f1`
if ( $instancepointer == "" ) then
	echo "400"
	echo "Bad request"
	echo "$this missing instance"
	exit 11
endif
set instance = `head -n $instancepointer $indata | tail -n1| cut -d'=' -f3|cut -d'"' -f2`

@ instancepointer += 2

set mpseppointers = `grep -n "\$mpsep" $indata | cut -d':' -f1`
set instancelines = 0
foreach mpseppointer ( $mpseppointers )
	if ( $mpseppointer >= $instancepointer ) then
		break;
	endif
end
@ mpseppointer-= 2
@ instancelines = $mpseppointer - $instancepointer
set outdata = $pdir/logs/$this.$$.out
head -n $mpseppointer $indata | tail -n $instancelines > $outdata
#Sanity check
if ( !( -e "$repo/$folder" ) ) then
	echo "404"
	echo "Not Found "
	echo "$this $repo/$folder"
	exit 2
endif
if (  -e "$repo/$folder/$instance" ) then
	echo "403"
	echo "Forbidden"
	echo "$this $repo/$folder/$instance"
	exit 3
endif
#actual validation command (TODO)
echo
set ret = $?
if ( $ret ) then
	echo "500"
	echo "Server error "
	echo "$this $ret"
	exit 4
endif
mv $outdata $repo/$folder/$instance
set ret = $?
if ( $ret ) then
	echo "500"
	echo "Server error "
	echo "$this $ret"
	exit 5
endif
echo "200"
echo "Success"
exit 0
